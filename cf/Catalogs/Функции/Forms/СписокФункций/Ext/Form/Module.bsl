
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если НЕ (Поле.Имя = "ТехническоеОписание" ИЛИ Поле.Имя = "ИнструкцияПользователя") Тогда
		Возврат;
	КонецЕсли; 
	
	СтандартнаяОбработка = Ложь;
	
	Файл = Элементы.Список.ТекущиеДанные[Поле.Имя];
	ИмяФайла = ТИ.ПолучитьПолноеОписаниеФайла(Файл);
	
	Адрес = ПолучитьФайлНаСервере(Файл, УникальныйИдентификатор);
	
	ПолучитьФайл(Адрес, ИмяФайла);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьФайлНаСервере(Файл, УникальныйИдентификатор)

	Возврат ПоместитьВоВременноеХранилище(Файл.ХранилищеФайла.Получить(), УникальныйИдентификатор);

КонецФункции // ПолучитьФайлНаСервере()

&НаКлиенте
Процедура ОтправитьСсылкуНаИнструкциюПоПочте(Команда)
	
	ТипФайла = ПредопределенноеЗначение("Перечисление.ТипыФайлов.ИнструкцияПользователя");
	
	ФункцияСсылка = Элементы.Список.ТекущиеДанные.Ссылка;
	Файл = ТИ.ПолучитьФайлФункцииНаСервере(ФункцияСсылка, ТипФайла);
	Если Файл = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не найден файл "+ТипФайла;
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	
	ОписаниеТипов = Новый ОписаниеТипов(Массив);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаЗначения", 
      ЭтотОбъект, Файл);	
 
    ПоказатьВводЗначения(
        Оповещение,
        , // пропускаем начальное значение
        "Выберите адресата",
        ОписаниеТипов
    );
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаЗначения(Результат, Параметры) Экспорт

	Физлицо = Результат;
	Файл = Параметры;
	Если Не Результат = Неопределено Тогда
		АдресЭлектроннойПочты = ТИ.ПолучитьРеквизитПоСсылке(Физлицо,"АдресЭлектроннойПочты");
		Если НЕ ЗначениеЗаполнено(АдресЭлектроннойПочты) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Для данного физ. лица не задан адрес электронной почты.";
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
		
		ШаблонСообщения = ПредопределенноеЗначение("Справочник.ШаблоныСообщений.СсылкаНаИнструкцию");
		
		ТИ.СформироватьИОтправитьУведомление(Файл, ШаблонСообщения, АдресЭлектроннойПочты);
	КонецЕсли;
	
	ПоказатьОповещениеПользователя("Сообщение подготовлено и будет отправлено в течении 5 минут.");
	
КонецПроцедуры // ПослеВводаЗначения()

&НаКлиенте
Процедура Инструкции(Команда)
	Форма = ОткрытьФорму("Справочник.Функции.Форма.СписокИнструкций");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ (ТИ.ДоступнаРоль("ПолныеПрава") ИЛИ ТИ.ДоступнаРоль("Разработчик")) Тогда
		
		Форма = ОткрытьФорму("Справочник.Функции.Форма.СписокИнструкций");
		Отказ = Истина;
	
	КонецЕсли; 
	
КонецПроцедуры
 
 
