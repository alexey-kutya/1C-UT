
&НаКлиенте
Процедура ОткрытьОтчет(Команда)
	
	КлючЗаписи = ЭтаФорма.Элементы.Список.ВыделенныеСтроки[0];
	СтруктураХранениеФайла = ПолучитьСтруктуруХраненияФайлаНаСервере(КлючЗаписи, УникальныйИдентификатор);
	
	Если СтруктураХранениеФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Прав(СтруктураХранениеФайла.Имя, 3) = "msg" Тогда
		ТекстВопроса = НСтр("ru = 'Этот файл является почтовым сообщением MS Outlook. В данной программе он не может быть открыт. Хотите получить почтовое сообщение с вложением этого файла на свою почту?'");
		ПараметрыОповещения = Новый Структура("СтруктураХранениеФайла, КлючЗаписи", СтруктураХранениеФайла, КлючЗаписи);
		Оповещение = Новый ОписаниеОповещения("ОбработкаВопроса", ЭтотОбъект, ПараметрыОповещения);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, НСтр("ru = 'Открытие файла'"));
		Возврат;
	КонецЕсли; 

	ПолучитьФайл(СтруктураХранениеФайла.Адрес, СтруктураХранениеФайла.Имя, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВопроса(Знач Результат, Знач ПараметрКоманды) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;	
	
	СтруктураХранениеФайла = ПараметрКоманды.СтруктураХранениеФайла;
	ДокументТестирования = ТИ.ПолучитьРеквизитПоСсылке(ПараметрКоманды.КлючЗаписи, "ДокументТестирования");
	
	СообщениеСтруктура = Новый Структура("ЗаголовокПисьма, ТекстПисьма", "Отправка:"+СтруктураХранениеФайла.Имя, "Сообщение содержит запрошенное вами письмо.");
	ФизЛицо = ТИ.ПолучитьРеквизитПоСсылке(ТИ.ТекущийПользователь(),"ФизЛицо");
	ТИ.СоздатьСообщение(ДокументТестирования, СообщениеСтруктура, ФизЛицо, ТИ.ПолучитьРеквизитПоСсылке(ФизЛицо, "АдресЭлектроннойПочты"), , СтруктураХранениеФайла.Имя, СтруктураХранениеФайла.Адрес);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруХраненияФайлаНаСервере(КлючЗаписи, УникальныйИдентификатор)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетыОТестировании.ХранимыйФайл,
		|	ОтчетыОТестировании.Имя
		|ИЗ
		|	РегистрСведений.ОтчетыОТестировании КАК ОтчетыОТестировании
		|ГДЕ
		|	ОтчетыОТестировании.Задача = &Задача
		|	И ОтчетыОТестировании.ДокументТестирования = &ДокументТестирования
		|	И ОтчетыОТестировании.ТипОтчета = &ТипОтчета";
	
	Запрос.УстановитьПараметр("Задача", КлючЗаписи.Задача);
	Запрос.УстановитьПараметр("ДокументТестирования", КлючЗаписи.ДокументТестирования);
	Запрос.УстановитьПараметр("ТипОтчета", КлючЗаписи.ТипОтчета);
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураХранениеФайла = Неопределено;
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Адрес = ПоместитьВоВременноеХранилище(ВыборкаДетальныеЗаписи.ХранимыйФайл.Получить(), УникальныйИдентификатор);
		СтруктураХранениеФайла = Новый Структура("Адрес, Имя", Адрес, ВыборкаДетальныеЗаписи.Имя);
	КонецЕсли; 
	
	Возврат СтруктураХранениеФайла;
	
КонецФункции // ()
  

